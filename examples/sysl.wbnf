// Sysl grammar

sysl_file -> import* EOL* (application EOL*)+;


// -------------- Imports --------------- //
import  -> "import"  prefix="/"{0,2} import=PATH ( "as"  APP_NAME)? ("~" mode=NAME)? EOL;



// -------------- Applications --------------- //
application ->  APPLICATION_NAME QSTRING?
        attribs? ":" COMMENT* INDENT app_decl:INDENT_SEP;

app_decl   -> SHORTCUT | table | wrap | endpoint | view;

// -------------- Types --------------- //

table -> mode=("!table"|"!type") NAME attribs?  ":" COMMENT*
    INDENT
        (COMMENT | (name=NAME "<:") type=type_spec optional="?"? attribs?):INDENT_SEP EOL;
wrap -> "!wrap" NAME attribs?  ":" COMMENT*
    INDENT
        (table | "!table"APP_NAME) :INDENT_SEP EOL;


type_spec -> (NativeDataType=("int32" | "int64" | "int" |
                             "float" | "string" | "date" |
                             "bool" | "decimal" | "datetime") | UserType=APP_NAME)
             SizeSpec=("(" \d+ (array=".."|sized=".")? \d* ")")?;
collection_type -> ("set" | "sequence") "of" type_spec;


// -------------- Endpoint     --------------- //

endpoint -> (rest_endpoint | simple_endpoint) EOL;

simple_endpoint -> SHORTCUT | (endpoint_name=(PATH) QSTRING? EVERYTHING? attribs? ":" (SHORTCUT | EVERYTHING));

rest_endpoint -> http_paths=("/" PATH) attribs? ":" INDENT (method_def | rest_endpoint)+:INDENT_SEP;

method_def -> HTTP_VERBS EVERYTHING? ":" EVERYTHING;

params -> "(" ( reference=(APP_NAME) | field ):"," ")";

field -> NAME (array_size? )?;

// --------------    views    --------------- //
view -> "!view" NAME "(" view_params? ")"
        ("->" retType=(collection_type|type_spec))?
        attribs? ":" ((INDENT expr_block:INDENT_SEP) | abstract="[~abstract]");

view_params -> (NAME "<:" (collection_type|type_spec)):",";

expr_block -> "";

// -------------- Common Rules --------------- //
attribs     ->  "[" @:"," "]"
             > pattern=("~" NAME:"+")
             | attr=(NAME "=" QSTRING)
        ;

array_size -> "(" DIGITS DOTDOT DIGITS? ")";

// -------------- Tokens --------------- //
NAME    -> [a-zA-Z_][-a-zA-Z0-9_]*;
PATH    -> [a-zA-Z0-9.:]*:"/";
DIGITS -> [1-9][0-9]*;
APP_NAME -> NAME:".";
SHORTCUT -> "...";
DOTDOT -> "..";
HTTP_VERBS -> ("GET" | "POST" | "DELETE" | "PUT" | "PATCH");
EVERYTHING -> /{[^\n]+};
APPLICATION_NAME    -> pkg=(NAME:"::" ".")? APP_NAME;

COMMENT -> "#" [^\n]* "\n";
EOL     -> COMMENT+ | [\_\t]*\n;
QSTRING -> /{"[^"]*"};

.wrapRE -> /{[\_]*()[\_]*};

INDENT      -> %INDENT="\n" \s+;
INDENT_SEP  -> %INDENT|\s+;
